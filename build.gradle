group 'io.github.aedans'
version '0.6.2'

buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply from: rootProject.file('gradle/generated-kotlin-sources.gradle')

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    maven { url 'https://dl.bintray.com/kategory/maven' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    def arrow_version = '0.6.1'
    compile "io.arrow-kt:arrow-core:$arrow_version"
    compile "io.arrow-kt:arrow-typeclasses:$arrow_version"
    compile "io.arrow-kt:arrow-instances:$arrow_version"
    compile "io.arrow-kt:arrow-free:$arrow_version"
    kapt    "io.arrow-kt:arrow-annotations-processor:$arrow_version"
    testCompile "io.arrow-kt:arrow-core:$arrow_version"
    testCompile "io.arrow-kt:arrow-typeclasses:$arrow_version"
    testCompile "io.arrow-kt:arrow-instances:$arrow_version"
    testCompile "io.arrow-kt:arrow-test:$arrow_version"
    kaptTest    "io.arrow-kt:arrow-annotations-processor:$arrow_version"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        publishToBintray(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['publishToBintray']
    publish = true

    pkg {
        repo = 'maven'
        name = 'katalyst'
        userOrg = user

        version {
            name = project.version
        }
    }
}
